		 .Z80
 		.RADIX 10
 		CSEG

;
;	THIS ROUTINE WILL SET UP ALL THE TASKS FOR THE GAME
;	THE FOLLOWING TASKS AND STUFF IS FOR DEMONSTRATION
;	PURPOSES AND CAN BE REMOVED BY THE PROGAMMER
;
		INCLUDE CLKAT.MRO
		INCLUDE	COMMON.MRO
		INCLUDE	LINKTAB.EXA

		EXTRN	PFLAG,HFLAG,RFLAG

JVAL::	DB	0
KVAL::	DB	0
TASK0::	SKLTCB
TASK1::	SKLTCB
TASK2::	SKLTCB

GTASK::		
	SETSK	JGET,TASK1,1,1
	SETSK	KGET,TASK2,1,1	
	SETSK	SHORT,TASK0,3,1
	RET
;
;	THIS IS A SHORT PROGRAM TO DEMONSTRATE THE USE OF THE 
;	SHELL PROGRAM
;
SHORT::	LD	A,(JVAL)	;CHECK JOYSTICK VALUE
	CP	10H		;WAS JOYSTICK FIRE BUTTON TOUCHED
	JR	Z,S2	
	LD	A,(KVAL)	;CHECK KEYBOARD VALUE
	CP	'C'		;CHECK FOR KEY
	RET	NZ		;NO - RETURN
S2:	LD	A,0
	LD	(PFLAG),A	;SET END OF PLAY FLAG TO 0
	RET
;
;	THIS TASK SUBROUTINE GETS DATA FROM THE JOYSTICK 
;
JGET::	N.DEVRDY	KEYBOARD,JSTCK1		;CHECK FOR DATA
	AND	A			;ANY DATA
	RET	Z			;NO - RETURN
	N.DEVIO	KEYBOARD,JSTCK1		;GET DATA
	LD	(JVAL),A		;SAVE IN JVAL
	RET

;
;	THIS TASK SUBOUTINE GETS DATA FROM THE KEYBOARD
;
KGET::	N.DEVRDY	KEYBOARD,KEYSTROKE 	;CHECK FOR DATA
	AND	A			;ANY DATA
	RET	Z			;NO - RETURN
	N.DEVIO	KEYBOARD,KEYSTROKE	;GET DATA
	CP	0BDH			;CHECK FOR RESTART
	JR	Z,K2
	CP	0ABH			
	JR	Z,K2
	CP	0EAH		 	;CHECK FOR HELP
	JR	Z,K3
	LD	(KVAL),A		;SAVE IN KVAL
	RET
K2:	LD	A,1
	LD	(RFLAG),A		;SET RESTART FLAG
	RET
K3:	LD	A,1
	LD	(HFLAG),A		;SET HELP FLAG
	RET

	END
